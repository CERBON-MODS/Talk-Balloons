plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${mapping_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = rootProject.file("../mod_version.txt").getText("UTF-8")
    group = maven_group

    repositories {
        maven {
            name "ParchmentMC"
            url "https://maven.parchmentmc.org"
        }
        maven {
            name "CurseMaven"
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven {
            name "ModMenu"
            url "https://maven.terraformersmc.com/"
        }
        maven {
            name "Ladysnake Mods" // Cardinal Components
            url "https://maven.ladysnake.org/releases"
        }
        maven {
            name 'ClothConfig'
            url "https://maven.shedaniel.me/"
        }
        maven {
            name 'SimpleVoiceChat'
            url 'https://maven.maxhenkel.de/repository/public'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding "UTF-8"
        options.release.set(17)
    }

    java {
        withSourcesJar()
    }
}

tasks.register("publishAllCurseforgeAndModrinth", Task) {
    dependsOn ":forge:publishCurseForgeAndModrinth"
    dependsOn ":fabric:publishCurseForgeAndModrinth"
}
